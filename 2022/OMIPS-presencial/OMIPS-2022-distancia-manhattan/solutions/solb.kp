iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción copia como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia;
      deja-zumbador;
    fin si-no
      avanza;
  fin;

  define-nueva-instrucción columna como inicio
    si frente-libre entonces inicio
      avanza;
      columna;
      si junto-a-zumbador entonces inicio
        copia;
        deja-zumbador;
      fin si-no
        avanza;
    fin si-no
      gira(2);
  fin;

  define-nueva-instrucción checa-columna(n) como inicio
    norte;
    columna;
    si no junto-a-zumbador entonces columna;
    si no junto-a-zumbador entonces inicio
      si si-es-cero(n) entonces
        este
      si-no
        oeste;

      si frente-libre entonces inicio
        avanza;
        checa-columna(n);
        si junto-a-zumbador entonces inicio
          copia;
          deja-zumbador;
        fin si-no
          avanza;
      fin si-no
        gira(2);
    fin si-no inicio
      si si-es-cero(n) entonces
        oeste
      si-no
        este;
    fin;
  fin;

  inicia-ejecución
    este;
    checa-columna(0);
    si no junto-a-zumbador entonces checa-columna(1);
    coge-zumbador;
    apágate;
  termina-ejecución
finalizar-programa
