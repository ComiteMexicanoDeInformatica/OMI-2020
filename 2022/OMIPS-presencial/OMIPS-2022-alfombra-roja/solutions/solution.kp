iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción copia-atras como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia-atras;
      deja-zumbador;
    fin si-no
      atras;
  fin;

  define-nueva-instrucción dfs como inicio
    si izquierda-libre entonces deja-zumbador;
    si frente-libre entonces deja-zumbador;
    si derecha-libre entonces deja-zumbador;
    si junto-a-zumbador entonces coge-zumbador;
    si izquierda-libre entonces inicio
      gira-izquierda;
      avanza;
      dfs;
      copia-atras;
      gira(3);
    fin;
    si frente-libre entonces inicio
      avanza;
      dfs;
      copia-atras;
    fin;
    si derecha-libre entonces inicio
      gira(3);
      avanza;
      dfs;
      copia-atras;
      gira(1);
    fin;
  fin;

  inicia-ejecución
    dfs;
    deja-zumbador;
    apágate;
  termina-ejecución
finalizar-programa
