iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción aprende como inicio
    si frente-libre entonces inicio
      avanza;
      si derecha-bloqueada entonces inicio
        aprende;
        si frente-libre entonces
          avanza
        si-no inicio
          gira(1);
          aprende;
          gira(1);
        fin;
      fin si-no inicio
        gira(3);
        avanza;
        si derecha-bloqueada entonces inicio
          aprende;
          gira(1);
        fin si-no inicio
          gira(3);
          avanza;
        fin;
      fin;
    fin si-no inicio
      gira(1);
      aprende;
      si frente-libre entonces inicio
        avanza;
        si derecha-libre entonces inicio
          gira(3);
          avanza;
        fin si-no inicio
          aprende;
          gira(1);
        fin;
      fin si-no inicio
        gira(1);
        aprende;
        si frente-libre entonces
          avanza
        si-no inicio
          gira(1);
          aprende;
          gira(1);
        fin;
      fin;
    fin;
  fin;

  inicia-ejecución
    gira-izquierda;
    aprende;
    apágate;
  termina-ejecución
finalizar-programa
