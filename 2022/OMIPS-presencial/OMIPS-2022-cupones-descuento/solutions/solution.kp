iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción cubetas como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cubetas;
      avanza;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        coge-zumbador;
        cubetas;
        deja-zumbador;
        oeste;
        pared;
        este;
      fin si-no
        este;
    fin;
  fin;

  define-nueva-instrucción ordena(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      ordena(n);
      deja(n);
      avanza;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        ordena(precede(n));
      fin si-no
        este;
    fin;
  fin;

  define-nueva-instrucción ordena-cubetas(n) como inicio
    si frente-libre entonces inicio
      avanza;
      ordena-cubetas(sucede(n));
    fin si-no inicio
      gira(2);
      ordena(n);
    fin;
  fin;

  define-nueva-instrucción ordena-renglon como inicio
    este;
    ultimo-zumbador;
    oeste;
    coge-zumbador;
    cubetas;
    deja-zumbador;
    oeste;
    pared;
    este;
    ordena-cubetas(1);
    oeste;
    pared;
  fin;

  define-nueva-instrucción resuelve como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      resuelve;
      si frente-libre entonces
        avanza
      si-no
        sur;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        resuelve;
        si no-orientado-al-sur entonces inicio
          coge-todo;
        fin;
      fin si-no inicio
        sur;
        avanza;
        este;
        espacio;
        oeste;
      fin;
    fin;
  fin;

  define-nueva-instrucción suma como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      suma;
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        suma;
      fin;
    fin;
  fin;

  inicia-ejecución
    ordena-renglon;
    norte;
    avanza;
    ordena-renglon;
    este;
    ultimo-zumbador;
    oeste;
    resuelve;
    si no orientado-al-sur entonces coge-todo;
    este;
    pared;
    oeste;
    suma;
    apágate;
  termina-ejecución
finalizar-programa
