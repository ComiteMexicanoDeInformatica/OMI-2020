iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción complemento(n) como inicio
    si no si-es-cero(n) entonces inicio
      si orientado-al-oeste entonces inicio
        coge-zumbador;
        si no junto-a-zumbador entonces sur;
        complemento(precede(n));
      fin si-no inicio
        deja-zumbador;
        complemento(precede(n));
      fin;
    fin;
  fin;

  define-nueva-instrucción cuenta-y-obten-complementos(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-obten-complementos(sucede(n));
    fin si-no inicio
      oeste;
      avanza;
      complemento(n);
      oeste;
      avanza;
      complemento(n);
    fin;
  fin;

  define-nueva-instrucción copia(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia(sucede(n));
      deja-zumbador;
    fin si-no inicio
      deja(n);
      avanza;
    fin;
  fin;

  define-nueva-instrucción valida como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      valida;
      si junto-a-zumbador entonces
        coge-zumbador
      si-no
        sur;
    fin si-no
      avanza;
  fin;

  define-nueva-instrucción deja-menor(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      deja-menor(precede(n));
      deja-zumbador;
    fin si-no
      coge-todo;
  fin;

  define-nueva-instrucción cuenta-y-deja-menor(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-deja-menor(sucede(n));
    fin si-no inicio
      avanza;
      deja-menor(n);
    fin;
  fin;

  inicia-ejecución
    copia(0);
    este;
    copia(0);
    sur;
    avanza;
    norte;
    copia(0);
    sur;
    avanza;
    este;
    avanza;
    norte;
    copia(0);
    oeste;
    valida;
    si orientado-al-sur entonces inicio
      avanza;
      coge-todo;
      oeste;
      avanza;
      coge-todo;
    fin si-no inicio
      sur;
      avanza;
      este;
      avanza;
      oeste;
      cuenta-y-obten-complementos(0);
      este;
      cuenta-y-deja-menor(0);
      atras;
      deja-zumbador;
    fin;
    apágate;
  termina-ejecución
finalizar-programa
