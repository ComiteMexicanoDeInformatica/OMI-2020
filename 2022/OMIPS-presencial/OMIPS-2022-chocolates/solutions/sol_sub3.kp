iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción paridad(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      paridad(sucede(n));
      gira(2);
    fin si-no inicio
      deja(n);
      avanza;
    fin;
  fin;

  define-nueva-instrucción copia como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia;
      deja-zumbador;
    fin si-no
      avanza;
  fin;

  define-nueva-instrucción mitad como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        mitad;
        deja-zumbador;
      fin;
    fin;
  fin;

  inicia-ejecución
    paridad(0);
    si orientado-al-sur entonces deja-zumbador;
    este;
    avanza;
    sur;
    avanza;
    norte;
    paridad(0);
    si orientado-al-sur entonces inicio
      oeste;
      avanza;
      si junto-a-zumbador entonces inicio
        atras;
        deja-zumbador;
      fin;
    fin;
    sur;
    avanza;
    oeste;
    pared;
    atras;
    copia;
    mitad;
    este;
    avanza;
    norte;
    avanza;
    sur;
    copia;
    apágate;
  termina-ejecución
finalizar-programa
