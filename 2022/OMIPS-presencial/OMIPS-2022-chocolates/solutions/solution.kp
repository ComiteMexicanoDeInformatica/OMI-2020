iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción recoge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción ve-inicio como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción quita(n) como inicio
    repetir n veces inicio
      si junto-a-zumbador entonces inicio
        coge-zumbador;
      fin si-no inicio
        sur;
      fin;
    fin;
  fin;

  define-nueva-instrucción divide(n) como inicio
    si junto-a-zumbador entonces inicio
      quita(n);
      si no-orientado-al-sur entonces inicio
        divide(n);
        deja-zumbador;
      fin;
    fin;
  fin;

  define-nueva-instrucción copia(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      deja(n);
      gira(2);
      avanza;
      gira(2);
    fin;
  fin;

  define-nueva-instrucción suma(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      suma(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      avanza;
      deja(n);
      gira(2);
      avanza;
      gira(2);
      copia(0);
      avanza;
    fin;
  fin;

  define-nueva-instrucción copias como inicio
    copia(0);
    este;
    avanza;
    norte;
    copia(0);
    este;
    avanza;
    copia(0);
    recoge-todo;
    avanza;
    norte;
    copia(0);
  fin;

  define-nueva-instrucción cuenta-y-divide(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-divide(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      divide(n);
    fin;
  fin;

  define-nueva-instrucción suma-y-divide como inicio
    suma(0);
    avanza;
    gira(2);
    cuenta-y-divide(0);
  fin;

  define-nueva-instrucción divide-y-suma(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      divide-y-suma(sucede(n));
    fin si-no inicio
      avanza;
      avanza;
      divide(n);
      oeste;
      avanza;
      divide(n);
      este;
      suma(0);
    fin;
  fin;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
      deja-zumbador;
    fin si-no inicio
      si junto-a-zumbador entonces norte;
    fin;
  fin;

  define-nueva-instrucción cuenta-y-compara(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-compara(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      compara(n);
    fin;
  fin;

  define-nueva-instrucción resta(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      resta(n);
      coge-zumbador;
    fin si-no inicio
      deja(n);
    fin;
  fin;

  define-nueva-instrucción inv-mod(n) como inicio
    compara(n);
    mientras orientado-al-norte hacer inicio
      quita(n);
      oeste;
      compara(n);
    fin;

    resta(n);
  fin;

  define-nueva-instrucción cuenta-y-resta(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-resta(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      avanza;
      inv-mod(n);
      oeste;
      avanza;
      inv-mod(n);
    fin;
  fin;

  define-nueva-instrucción calcula-faltante como inicio
    este;
    avanza;
    oeste;
    cuenta-y-resta(0);
  fin;

  define-nueva-instrucción compara-y-deja-menor(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      compara-y-deja-menor(sucede(n));
    fin si-no inicio
      avanza;
      compara(n);

      si orientado-al-norte entonces inicio
        recoge-todo;
        deja(n);
      fin;
    fin;
  fin;

  define-nueva-instrucción copia-a-inicio como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia-a-inicio;
      deja-zumbador;
    fin si-no inicio
      ve-inicio;
      recoge-todo;
    fin;
  fin;

  inicia-ejecución
    copias;

    ve-inicio;
    este;
    suma-y-divide;

    norte;
    avanza;
    este;
    avanza;
    oeste;
    divide-y-suma(0);

    sur;
    cuenta-y-compara(0);

    si orientado-al-norte entonces inicio
      calcula-faltante;
      este;
      compara-y-deja-menor(0);
      este;
      avanza;
      copia-a-inicio;
    fin si-no inicio
      copia-a-inicio;
      este;
      avanza;
      recoge-todo;
    fin;
  termina-ejecución
finalizar-programa
