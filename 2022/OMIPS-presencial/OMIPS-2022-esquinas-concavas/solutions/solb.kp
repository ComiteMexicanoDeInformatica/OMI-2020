iniciar-programa
  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción super-avanza como inicio
    si derecha-libre entonces inicio
      gira(3);
      avanza;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
      fin si-no inicio
        gira-izquierda;
        si frente-libre y no junto-a-zumbador entonces avanza;
      fin;
    fin;
  fin;

  define-nueva-instrucción cuenta-esquinas(n) como inicio
    super-avanza;
    si no junto-a-zumbador entonces inicio
      si frente-bloqueado y derecha-bloqueada entonces inicio
        cuenta-esquinas(n);
        deja-zumbador;
      fin si-no
        cuenta-esquinas(n);
    fin si-no inicio
      si (no orientado-al-norte y si-es-cero(n)) o
          (no orientado-al-este y si-es-cero(precede(n))) o
          (no orientado-al-sur y si-es-cero(precede(precede(n)))) o
          (no orientado-al-oeste y si-es-cero(precede(precede(precede(n)))))
      entonces
        si frente-libre entonces
          cuenta-esquinas(n)
        si-no inicio
          cuenta-esquinas(n);
          deja-zumbador;
        fin;
    fin;
  fin;

  inicia-ejecución
    deja-zumbador;
    si orientado-al-norte entonces
      cuenta-esquinas(0)
    si-no si orientado-al-este entonces
      cuenta-esquinas(1)
    si-no si orientado-al-sur entonces
      cuenta-esquinas(2)
    si-no
      cuenta-esquinas(3);
    si frente-libre entonces coge-zumbador;
    apágate;
  termina-ejecución
finalizar-programa
