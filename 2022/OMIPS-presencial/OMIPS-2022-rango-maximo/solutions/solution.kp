iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción cubetas como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cubetas;
      avanza;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        coge-zumbador;
        cubetas;
        deja-zumbador;
        oeste;
        pared;
        este;
      fin si-no
        este;
    fin;
  fin;

  define-nueva-instrucción copia(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      deja(n);
    fin;
  fin;

  define-nueva-instrucción procesa-cubeta(n) como inicio
    norte;
    copia(0);
    repetir n veces inicio
      si no orientado-al-sur entonces inicio
        norte;
        copia(0);
        sur;
        avanza;
        este;
        si frente-libre entonces
          copia(0)
        si-no
          sur;
      fin;
    fin;
    oeste;
    mientras junto-a-zumbador hacer inicio
      coge-todo;
      si frente-libre entonces inicio
        avanza;
        si no junto-a-zumbador entonces atras;
      fin;
    fin;
    sur;
    avanza;
    este;
  fin;

  define-nueva-instrucción resuelve(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      resuelve(sucede(n));
    fin si-no inicio
      sur;
      avanza;
      este;
      si junto-a-zumbador entonces procesa-cubeta(n);
      mientras frente-libre hacer inicio
        avanza;
        si junto-a-zumbador entonces procesa-cubeta(n);
      fin;
    fin;
  fin;

  define-nueva-instrucción deja-mayor(n) como inicio
    si junto-a-zumbador o no si-es-cero(n) entonces inicio
      si junto-a-zumbador entonces coge-zumbador;
      si si-es-cero(n) entonces
        deja-mayor(n)
      si-no
        deja-mayor(precede(n));
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción aprende-y-deja-mayor(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-y-deja-mayor(sucede(n));
    fin si-no inicio
      avanza;
      deja-mayor(n);
    fin;
  fin;

  inicia-ejecución
    este;
    ultimo-zumbador;
    oeste;
    coge-zumbador;
    cubetas;
    deja-zumbador;
    oeste;
    pared;
    norte;
    avanza;
    resuelve(0);
    norte;
    avanza;
    avanza;
    oeste;
    mientras frente-libre hacer aprende-y-deja-mayor(0);
    sur;
    copia(0);
    avanza;
    coge-todo;
    atras;
    copia(0);
    apágate;
  termina-ejecución
finalizar-programa
