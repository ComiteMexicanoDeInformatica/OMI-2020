iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción ultimo-zumbador como inicio
    espacio;
    si no junto-a-zumbador entonces atras;
  fin;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción cubetas como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cubetas;
      avanza;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        coge-zumbador;
        cubetas;
        deja-zumbador;
        oeste;
        pared;
        este;
      fin si-no
        este;
    fin;
  fin;

  define-nueva-instrucción deja-mayor(n) como inicio
    si junto-a-zumbador o no si-es-cero(n) entonces inicio
      si junto-a-zumbador entonces coge-zumbador;
      si si-es-cero(n) entonces
        deja-mayor(n)
      si-no
        deja-mayor(precede(n));
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción aprende-y-deja-mayor(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-y-deja-mayor(sucede(n));
    fin si-no inicio
      avanza;
      deja-mayor(n);
    fin;
  fin;

  define-nueva-instrucción acumula(n) como inicio
    si no si-es-cero(n) entonces inicio
      si junto-a-zumbador entonces inicio
        avanza;
        acumula(precede(n));
        deja-zumbador;
      fin si-no inicio
        avanza;
        acumula(precede(n));
      fin;
    fin;
  fin;

  define-nueva-instrucción cuenta-y-resuelve(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-resuelve(sucede(n));
    fin si-no inicio
      sur;
      avanza;
      este;
      repetir precede(n) veces avanza;
      mientras no orientado-al-sur hacer inicio
        oeste;
        acumula(precede(n));
        este;
        repetir n veces inicio
          si frente-libre entonces
            avanza
          si-no
            sur;
        fin;
      fin;
      oeste;
      mientras frente-libre hacer aprende-y-deja-mayor(0);
    fin;
  fin;

  inicia-ejecución
    este;
    ultimo-zumbador;
    oeste;
    coge-zumbador;
    cubetas;
    deja-zumbador;
    oeste;
    pared;
    norte;
    avanza;
    cuenta-y-resuelve(0);
    apágate;
  termina-ejecución
finalizar-programa
