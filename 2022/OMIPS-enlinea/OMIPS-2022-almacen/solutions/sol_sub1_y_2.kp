iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción super-avanza como inicio
    si frente-libre entonces
      avanza
    si-no inicio
      si izquierda-libre entonces inicio
        gira(1);
        avanza;
      fin si-no inicio
        gira(3);
        avanza;
      fin;
    fin;
  fin;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y
           (frente-libre o izquierda-libre o derecha-libre)
    hacer super-avanza;

  define-nueva-instrucción resuelve como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      resuelve;
      deja-zumbador;
      super-avanza;
    fin si-no inicio
      mientras frente-bloqueado hacer gira-izquierda;
      zumbador;
      gira(2);
      si junto-a-zumbador entonces super-avanza;
    fin;
  fin;

  inicia-ejecución
    resuelve;
    apágate;
  termina-ejecución
finalizar-programa
