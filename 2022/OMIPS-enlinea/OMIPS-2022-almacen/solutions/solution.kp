iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción copia como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia;
      deja-zumbador;
    fin si-no inicio
      deja-zumbador;
      avanza;
    fin;
  fin;

  define-nueva-instrucción copia-atras(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia-atras(sucede(n));
    fin si-no inicio
      deja-zumbador;
      atras;
      coge-zumbador;
      deja(precede(n));
    fin;
  fin;

  define-nueva-instrucción dfs como inicio
    si junto-a-zumbador entonces inicio
      gira(3);
      repetir 3 veces inicio
        si frente-libre entonces inicio
          avanza;
          si no junto-a-zumbador entonces inicio
            atras;
            copia;
            dfs;
            si junto-a-zumbador entonces
              copia-atras(0)
            si-no inicio
              atras;
              coge-zumbador;
            fin;
          fin si-no
            atras;
        fin;
        gira-izquierda;
      fin;
      gira(2);
    fin;
  fin;

  define-nueva-instrucción resuelve(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      resuelve(sucede(n));
    fin si-no inicio
      pared;
      gira(2);
      mientras junto-a-zumbador hacer avanza;
      deja(n);
      dfs;
    fin;
  fin;

  inicia-ejecución
    resuelve(0);
    apágate;
  termina-ejecución
finalizar-programa
