iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción ultimo-zumbador como inicio
    mientras junto-a-zumbador y frente-libre hacer avanza;
    gira(2);
    si no junto-a-zumbador entonces avanza;
  fin;

  define-nueva-instrucción resuelve(n) como inicio
    si junto-a-zumbador y si-es-cero(n) entonces inicio
      coge-zumbador;
      resuelve(n);
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces
        avanza
      si-no inicio
        si orientado-al-norte entonces inicio
          este;
          si frente-libre entonces inicio
            avanza;
            sur;
          fin;
        fin si-no inicio
          este;
          si frente-libre entonces inicio
            avanza;
            norte;
          fin;
        fin;
      fin;
      si no orientado-al-este entonces inicio
        si si-es-cero(n) entonces
          resuelve(1)
        si-no
          resuelve(0);
      fin si-no inicio
        sur;
        pared;
        oeste;
        pared;
        coge-todo;
      fin;
    fin;
  fin;

  define-nueva-instrucción inicializa(n) como inicio
    si orientado-al-sur entonces inicio
      si frente-libre entonces inicio
        avanza;
        si si-es-cero(n) entonces
          inicializa(1)
        si-no
          inicializa(0);
      fin si-no inicio
        oeste;
        inicializa(n);
      fin;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        si si-es-cero(n) entonces
          inicializa(1)
        si-no
          inicializa(0);
      fin si-no inicio
        norte;
        resuelve(n);
      fin;
    fin;
  fin;

  inicia-ejecución
    sur;
    inicializa(0);
    apágate;
  termina-ejecución
finalizar-programa
