iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción ultimo-zumbador como inicio
    mientras junto-a-zumbador y frente-libre hacer avanza;
    gira(2);
    si no junto-a-zumbador entonces avanza;
  fin;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
      deja-zumbador;
    fin si-no inicio
      si junto-a-zumbador entonces
        norte
      si-no inicio
        si no si-es-cero(n) entonces sur;
      fin;
    fin;
  fin;

  define-nueva-instrucción divide(n) como inicio
    si junto-a-zumbador entonces inicio
      repetir n veces
        si junto-a-zumbador entonces
          coge-zumbador
        si-no
          sur;
      si no orientado-al-sur entonces inicio
        divide(n);
        deja-zumbador;
      fin;
    fin;
  fin;

  define-nueva-instrucción aprende-y-copia(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-y-copia(sucede(n));
      deja-zumbador;
    fin si-no inicio
      deja(n);
      si frente-libre entonces inicio
        avanza;
        aprende-y-copia(0);
        avanza;
      fin si-no inicio
        norte;
        avanza;
        este;
      fin;
    fin;
  fin;

  define-nueva-instrucción copia-linea como inicio
    este;
    ultimo-zumbador;
    aprende-y-copia(0);
  fin;

  define-nueva-instrucción limpia-linea como inicio
    este;
    ultimo-zumbador;
    coge-todo;
    mientras frente-libre hacer inicio
      avanza;
      coge-todo;
    fin;
  fin;

  define-nueva-instrucción llena-linea(n) como inicio
    este;
    ultimo-zumbador;
    coge-todo;
    deja(n);
    mientras frente-libre hacer inicio
      avanza;
      coge-todo;
      deja(n);
    fin;
  fin;

  define-nueva-instrucción resta-inicial(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      resta-inicial(sucede(n));
      si junto-a-zumbador entonces coge-zumbador;
    fin si-no
      repetir 2 veces avanza;
  fin;

  define-nueva-instrucción deja-resultado(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      deja-resultado(sucede(n));
      si orientado-al-sur entonces inicio
        si junto-a-zumbador entonces
          coge-zumbador
        si-no inicio
          norte;
          deja-zumbador;
        fin;
      fin si-no
        deja-zumbador;
    fin si-no inicio
      sur;
      avanza;
    fin;
  fin;

  define-nueva-instrucción aprende-y-deja-limite(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-y-deja-limite(sucede(n));
    fin si-no inicio
      deja(n);
      esquina;
      llena-linea(n);
    fin;
  fin;

  define-nueva-instrucción quita-con-signo(n) como inicio
    repetir n veces inicio
      si junto-a-zumbador y orientado-al-este entonces
        coge-zumbador
      si-no inicio
        si orientado-al-este entonces inicio
          sur;
          avanza;
          deja-zumbador;
        fin si-no
          deja-zumbador;
      fin;
    fin;

    si orientado-al-este entonces inicio
      oeste;
      avanza;
    fin si-no inicio
      norte;
      avanza;
      oeste;
      avanza;
    fin;
  fin;

  define-nueva-instrucción aprende-y-resta(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-y-resta(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      quita-con-signo(n);
    fin;
  fin;

  define-nueva-instrucción maximo-busqueda(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      maximo-busqueda(sucede(n));
      coge-zumbador;
    fin si-no inicio
      deja(n);
      avanza;
    fin;
  fin;

  define-nueva-instrucción copia-maximo(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia-maximo(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      deja(n);
      este;
      avanza;
      avanza;
      deja(sucede(n));
    fin;
  fin;

  define-nueva-instrucción inicializa-busqueda-binaria como inicio
    norte;
    avanza;
    este;
    maximo-busqueda(0);
    norte;
    copia-maximo(0);
    oeste;
    pared;
    este;
  fin;

  define-nueva-instrucción calcula-cambio como inicio
    avanza;
    este;
    aprende-y-resta(0);
  fin;

  define-nueva-instrucción aprende-y-compara(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-y-compara(sucede(n));
    fin si-no inicio
      deja(n);
      repetir 2 veces avanza;
      deja(n);
      atras;
      compara(n);
    fin;
  fin;

  define-nueva-instrucción compara-ini-y-fin como inicio
    aprende-y-compara(0);
  fin;

  define-nueva-instrucción haz-promedio(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      haz-promedio(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      deja(n);
      divide(2);
    fin;
  fin;

  define-nueva-instrucción busqueda-binaria como inicio
    compara-ini-y-fin;
    si no orientado-al-sur entonces inicio
      haz-promedio(0);
    fin;
  fin;

  inicia-ejecución
    norte;
    avanza;
    copia-linea;
    copia-linea;
    repetir 2 veces inicio
      sur;
      avanza;
      limpia-linea;
    fin;

    esquina;
    este;
    copia-linea;
    copia-linea;

    norte;
    avanza;
    oeste;
    pared;
    atras;
    aprende-y-deja-limite(0);
    norte;
    avanza;
    limpia-linea;
    norte;
    avanza;
    este;
    ultimo-zumbador;
    mientras frente-libre hacer calcula-cambio;
    coge-todo;

    inicializa-busqueda-binaria;

    busqueda-binaria;

    apágate;
  termina-ejecución
finalizar-programa
