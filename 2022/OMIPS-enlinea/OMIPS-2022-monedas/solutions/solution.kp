iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción hazPareja como inicio
    coge-zumbador;
    avanza;
    coge-zumbador;
    si junto-a-zumbador entonces inicio
      atras;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        sur;
      fin;
    fin si-no inicio
      atras;
      si no junto-a-zumbador entonces inicio
        deja-zumbador;
        sur;
      fin;
    fin;
    deja-zumbador;
    si orientado-al-sur entonces inicio
      este;
      avanza;
      sur;
    fin si-no
      avanza;
    deja-zumbador;
  fin;

  define-nueva-instrucción cuenta como inicio
    si junto-a-zumbador entonces inicio
      hazPareja;
      si orientado-al-sur entonces inicio
        este;
        si frente-libre entonces inicio
          avanza;
          cuenta;
          deja-zumbador;
        fin si-no inicio
          esquina;
          deja-zumbador;
        fin;
      fin si-no inicio
        si frente-libre entonces inicio
          avanza;
          cuenta;
        fin si-no inicio
          esquina;
        fin;
      fin;
    fin si-no
      esquina;
  fin;

  inicia-ejecución
    este;
    avanza;
    cuenta;
    apágate;
  termina-ejecución
finalizar-programa
