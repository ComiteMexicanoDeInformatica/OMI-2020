iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador hacer avanza;

  define-nueva-instrucción diagonal como inicio
    gira(1);
    avanza;
    gira(3);
    avanza;
    si no junto-a-zumbador entonces inicio
      deja-zumbador;
      diagonal;
    fin;
  fin;

  define-nueva-instrucción super-avanza como inicio
    si frente-libre entonces
      avanza
    si-no inicio
      sur;
      pared;
      este;
      si frente-libre entonces inicio
        avanza;
        norte;
      fin;
    fin;
  fin;

  inicia-ejecución
    mientras no junto-a-zumbador hacer super-avanza;
    este;
    diagonal;
    sur;
    avanza;
    zumbador;
    gira(2);
    diagonal;
    apágate;
  termina-ejecución
finalizar-programa
