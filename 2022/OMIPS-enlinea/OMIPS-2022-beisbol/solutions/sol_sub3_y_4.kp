iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción revisa como inicio
    oeste;
    avanza;
    zumbador;
    este;
    si junto-a-zumbador entonces inicio
      avanza;
      zumbador;
      apágate;
    fin si-no inicio
      avanza;
      zumbador;
      norte;
      avanza;
      deja-zumbador;
      revisa;
    fin;
  fin;

  define-nueva-instrucción super-avanza como inicio
    si frente-libre entonces
      avanza
    si-no inicio
      oeste;
      pared;
      norte;
      si frente-libre entonces inicio
        avanza;
        este;
      fin;
    fin;
  fin;

  inicia-ejecución
    este;
    mientras no junto-a-zumbador hacer super-avanza;
    norte;
    revisa;
  termina-ejecución
finalizar-programa
