iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción expande como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      expande;
      deja-zumbador;
      avanza;
    fin si-no inicio
      norte;
    fin;
  fin;

  define-nueva-instrucción recorre como inicio
    mientras orientado-al-norte y junto-a-zumbador hacer avanza;
    este;
    mientras frente-libre y no junto-a-zumbador hacer avanza;
    si frente-libre entonces inicio
      norte;
      recorre;
    fin;
  fin;

  define-nueva-instrucción limpia como inicio
    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces
        coge-zumbador
      si-no
        deja-zumbador;
      avanza;
    fin;
  fin;

  define-nueva-instrucción marca-maximos como inicio
    mientras frente-libre hacer inicio
      avanza;
      si junto-a-zumbador entonces inicio
        sur;
        pared;
        coge-zumbador;
        norte;
        avanza;
        mientras junto-a-zumbador hacer avanza;
        atras;
        oeste;
      fin;
    fin;
    sur;
    pared;
    este;
  fin;

  inicia-ejecución
    este;
    mientras junto-a-zumbador hacer inicio
      expande;
      sur;
      pared;
      este;
      avanza;
    fin;
    uno-uno;
    recorre;
    sur;
    avanza;
    oeste;
    marca-maximos;
    limpia;
    uno-uno;
  termina-ejecución
finalizar-programa
