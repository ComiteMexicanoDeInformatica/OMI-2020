iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción quita-si-puedes(n) como inicio
    repetir n veces inicio
      si junto-a-zumbador entonces
        coge-zumbador
      si-no
        sur;
    fin;
  fin;

  define-nueva-instrucción divide(n) como inicio
    quita-si-puedes(n);
    si no-orientado-al-sur entonces inicio
      divide(n);
      deja-zumbador;
    fin si-no
      este;
  fin;

  define-nueva-instrucción obten-restas(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      obten-restas(sucede(n));
      si junto-a-zumbador entonces coge-zumbador;
    fin si-no inicio
      deja(n);
      avanza;
      si junto-a-zumbador entonces inicio
        obten-restas(0);
        avanza;
      fin si-no inicio
        gira(2);
      fin;
    fin;
  fin;

  define-nueva-instrucción acumula-resultado como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      acumula-resultado;
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        acumula-resultado;
      fin;
    fin;
  fin;

  define-nueva-instrucción cuenta-salto(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-salto(sucede(n));
    fin si-no inicio
      sur;
      avanza;
      este;
      avanza;
      mientras junto-a-zumbador hacer inicio
        coge-zumbador;
        divide(n);
        avanza;
      fin;
      gira(2);
      acumula-resultado;
    fin;
  fin;

  inicia-ejecución
    este;
    obten-restas(0);
    avanza;
    coge-todo;
    norte;
    avanza;
    cuenta-salto(0);
    apágate;
  termina-ejecución
finalizar-programa
