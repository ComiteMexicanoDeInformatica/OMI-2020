iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción checa-final como inicio
    si frente-libre entonces inicio
      avanza;
      si junto-a-zumbador entonces
        atras
      si-no
        norte;
    fin si-no
      norte;
  fin;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
      deja-zumbador;
    fin si-no inicio
      si junto-a-zumbador entonces norte;
    fin;
  fin;

  define-nueva-instrucción cuenta-y-compara(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-compara(sucede(n));
    fin si-no inicio
      deja(n);
      avanza;
      compara(n);
      si orientado-al-norte entonces inicio
        avanza;
        deja(1);
        oeste;
        avanza;
        sur;
        avanza;
        este;
      fin;
    fin;
  fin;

  define-nueva-instrucción cuenta-salto(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-salto(sucede(n));
    fin si-no inicio
      sur;
      avanza;
      este;
      checa-final;
      mientras orientado-al-este hacer inicio
        deja(n);
        cuenta-y-compara(0);
        checa-final;
      fin;
    fin;
  fin;

  define-nueva-instrucción acumula-resultado como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      acumula-resultado;
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        acumula-resultado;
      fin si-no inicio
        sur;
        avanza;
        coge-todo;
      fin;
    fin;
  fin;

  inicia-ejecución
    avanza;
    cuenta-salto(0);
    avanza;
    oeste;
    acumula-resultado;
    apágate;
  termina-ejecución
finalizar-programa
