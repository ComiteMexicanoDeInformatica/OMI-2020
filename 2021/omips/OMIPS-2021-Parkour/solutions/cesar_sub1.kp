iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción obten-restas(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      obten-restas(sucede(n));
      si junto-a-zumbador entonces coge-zumbador;
    fin si-no inicio
      deja(n);
      avanza;
      si junto-a-zumbador entonces inicio
        obten-restas(0);
        avanza;
      fin si-no inicio
        gira(2);
      fin;
    fin;
  fin;

  define-nueva-instrucción acumula como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      acumula;
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        acumula;
      fin;
    fin;
  fin;

  inicia-ejecución
    este;
    obten-restas(0);
    avanza;
    coge-todo;
    este;
    mientras frente-libre hacer inicio
      avanza;
      si junto-a-zumbador entonces coge-zumbador;
    fin;
    oeste;
    acumula;
    apágate;
  termina-ejecución
finalizar-programa
