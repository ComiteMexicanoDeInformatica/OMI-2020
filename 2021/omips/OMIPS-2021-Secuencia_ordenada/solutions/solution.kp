iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción apaga-OK como inicio
    norte;
    apágate;
  fin;

  define-nueva-instrucción apaga-err como inicio
    sur;
    apágate;
  fin;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
      deja-zumbador;
    fin si-no inicio
      si no junto-a-zumbador y si-es-cero(n) entonces
        este
      si-no inicio
        si no junto-a-zumbador entonces
          norte
        si-no
          sur;
      fin;
    fin;
  fin;

  define-nueva-instrucción cuenta-y-compara(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-compara(sucede(n));
    fin si-no inicio
      este;
      si frente-bloqueado entonces inicio
        apaga-OK;
      fin;
      avanza;
      si junto-a-zumbador entonces inicio
        compara(n);
      fin si-no
        oeste;
    fin;
  fin;

  define-nueva-instrucción baja como inicio
    cuenta-y-compara(0);
    si orientado-al-oeste entonces
      apaga-OK
    si-no inicio
      si orientado-al-sur entonces
        apaga-err
      si-no
        baja;
    fin;
  fin;

  define-nueva-instrucción sube como inicio
    cuenta-y-compara(0);
    si orientado-al-oeste entonces
      apaga-OK
    si-no inicio
      si orientado-al-norte entonces
        baja
      si-no
        sube;
    fin;
  fin;

  inicia-ejecución
    sube;
    apágate;
  termina-ejecución
finalizar-programa
