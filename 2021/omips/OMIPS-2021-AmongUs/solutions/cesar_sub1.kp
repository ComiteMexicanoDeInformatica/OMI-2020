iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción checa-impostor(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      checa-impostor(sucede(n));
    fin si-no inicio
      deja-zumbador;
      este;
      repetir n veces avanza;
      oeste;
      si no-junto-a-zumbador entonces inicio
        repetir 2 veces repetir n veces avanza;
        este;
        si no junto-a-zumbador entonces inicio
          repetir n veces avanza;
          apágate;
        fin si-no
          repetir n veces avanza;
      fin si-no inicio
        repetir n veces avanza;
        este;
      fin;
    fin;
  fin;

  inicia-ejecución
    este;
    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces inicio
        checa-impostor(0);
      fin;
      avanza;
    fin;
    apágate;
  termina-ejecución
finalizar-programa
