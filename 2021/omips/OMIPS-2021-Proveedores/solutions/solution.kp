iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción procesa-columna como inicio
    si frente-libre entonces inicio
      avanza;
      si junto-a-zumbador entonces inicio
        procesa-columna;
        deja-zumbador;
      fin si-no
        procesa-columna;
    fin si-no inicio
      gira(2);
      pared;
      este;
    fin;
  fin;

  define-nueva-instrucción procesa-columnas como inicio
    mientras frente-libre hacer inicio
      avanza;
      norte;
      procesa-columna;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        si junto-a-zumbador entonces
          coge-todo
        si-no
          deja-zumbador;
      fin;
    fin;
  fin;

  define-nueva-instrucción marca como inicio
    si frente-libre entonces inicio
      avanza;
      marca;
      avanza;
    fin si-no inicio
      deja-zumbador;
      gira(2);
    fin;
  fin;

  define-nueva-instrucción marca-proveedor como inicio
    norte;
    avanza;
    mientras no-junto-a-zumbador hacer avanza;
    oeste;
    marca;
    sur;
    pared;
    oeste;
  fin;

  define-nueva-instrucción marca-proveedores como inicio
    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces marca-proveedor;
      avanza;
    fin;
  fin;

  define-nueva-instrucción cuenta-proveedores como inicio
    si junto-a-zumbador entonces inicio
      coge-todo;
      cuenta-proveedores;
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        cuenta-proveedores;
      fin si-no inicio
        gira(2);
        pared;
      fin;
    fin;
  fin;

  inicia-ejecución
    este;
    procesa-columnas;
    oeste;
    marca-proveedores;
    norte;
    cuenta-proveedores;
    apágate;
  termina-ejecución
finalizar-programa
