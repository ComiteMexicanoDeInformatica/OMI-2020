iniciar-programa
  define-prototipo-instrucción junta(n);

  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción super-avanza como inicio
    si frente-libre entonces
      avanza
    si-no inicio
      sur;
      mientras no junto-a-zumbador hacer avanza;
      atras;
      este;
      si frente-libre entonces inicio
        avanza;
        norte;
      fin;
    fin;
  fin;

  define-nueva-instrucción mide-fuerza(n) como inicio
    avanza;
    oeste;
    avanza;
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      si junto-a-zumbador entonces inicio
        uno-uno;
        coge-todo;
        deja(sucede(n));
        apágate;
      fin;
      deja-zumbador;
    fin;
    atras;
    sur;
    atras;

    avanza;
    si junto-a-zumbador entonces atras;
    oeste;
    avanza;
    si junto-a-zumbador entonces atras;
    sur;
    mide-fuerza(sucede(n));
  fin;

  define-nueva-instrucción checa(n) como inicio
    si frente-libre y no junto-a-zumbador entonces inicio
      avanza;
      checa(sucede(n));
      avanza;
    fin si-no inicio
      si junto-a-zumbador entonces inicio
        uno-uno;
        deja(n);
        apágate;
      fin si-no
        gira(2);
    fin;
  fin;

  define-nueva-instrucción frontera como inicio
    si frente-libre entonces inicio
      deja-zumbador;
      avanza;
      frontera;
      avanza;
    fin si-no inicio
      deja-zumbador;
      gira(2);
    fin;
  fin;

  inicia-ejecución
    checa(0);
    este;
    checa(0);
    norte;
    frontera;
    este;
    frontera;
    norte;
    avanza;
    este;
    avanza;
    norte;
    mientras no junto-a-zumbador hacer super-avanza;
    sur;
    mide-fuerza(0);
    apágate;
  termina-ejecución
finalizar-programa
