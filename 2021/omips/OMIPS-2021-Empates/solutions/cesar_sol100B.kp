iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
    fin si-no inicio
      si no junto-a-zumbador entonces sur;
      coge-todo;
    fin;
  fin;

  define-nueva-instrucción abs-diferencia(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      abs-diferencia(precede(n));
    fin si-no inicio
      si no junto-a-zumbador entonces deja(n);
    fin;
  fin;

  define-nueva-instrucción copia-fila(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia-fila(sucede(n));
      deja-zumbador;
    fin si-no inicio
      deja(n);
      si frente-libre entonces inicio
        avanza;
        si junto-a-zumbador entonces inicio
          copia-fila(0);
          avanza;
        fin si-no inicio
          norte;
          avanza;
          oeste;
          avanza;
        fin;
      fin si-no inicio
        norte;
        avanza;
        oeste;
      fin;
    fin;
  fin;

  define-nueva-instrucción abs-fila(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      abs-fila(sucede(n));
    fin si-no inicio
      avanza;
      mientras junto-a-zumbador hacer inicio
        abs-diferencia(n);
        si frente-libre entonces avanza;
      fin;
    fin;
  fin;

  define-nueva-instrucción matriz-de-filas como inicio
    mientras junto-a-zumbador hacer inicio
      avanza;
      copia-fila(0);
      este;
    fin;
  fin;

  define-nueva-instrucción matriz-de-abs(n) como inicio
    repetir n veces avanza;
    si junto-a-zumbador entonces inicio
      abs-fila(0);
      oeste;
      pared;
      norte;
      avanza;
      este;
      matriz-de-abs(sucede(n));
    fin;
  fin;

  define-nueva-instrucción cuenta-menores-o-iguales(n) como inicio
    si junto-a-zumbador entonces inicio
      compara(n);
      si orientado-al-sur entonces inicio
        este;
        cuenta-menores-o-iguales(n);
        deja-zumbador;
      fin si-no
        cuenta-menores-o-iguales(n);
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        cuenta-menores-o-iguales(n);
      fin si-no inicio
        si izquierda-libre entonces inicio
          oeste;
          pared;
          norte;
          avanza;
          este;
          cuenta-menores-o-iguales(n);
        fin si-no inicio
          uno-uno;
        fin;
      fin;
    fin;
  fin;

  define-nueva-instrucción aprende-error-y-resuelve(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-error-y-resuelve(sucede(n));
    fin si-no inicio
      sur;
      avanza;
      este;
      matriz-de-filas;
      uno-uno;
      este;
      matriz-de-abs(0);
      uno-uno;
      este;
      cuenta-menores-o-iguales(n);
    fin;
  fin;

  inicia-ejecución
    avanza;
    aprende-error-y-resuelve(0);
  termina-ejecución
finalizar-programa
