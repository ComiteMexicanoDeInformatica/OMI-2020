iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    este;
  fin;

  define-nueva-instrucción cubetas como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cubetas;
      avanza;
    fin si-no inicio
      avanza;
      si junto-a-zumbador entonces inicio
        cubetas;
        atras;
        deja-zumbador;
        esquina;
      fin si-no
        esquina;
    fin;
  fin;

  define-nueva-instrucción multiplica(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      multiplica(n);
      deja(n);
    fin;
  fin;

  define-nueva-instrucción divide(n) como inicio
    si junto-a-zumbador entonces inicio
      repetir n veces coge-zumbador;
      divide(n);
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción parejas-propias(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      parejas-propias(sucede(n));
    fin si-no inicio
      deja(n);
      norte;
      avanza;
      deja(n);
      multiplica(precede(n));
      divide(2);
      sur;
      avanza;
      este;
    fin;
  fin;

  define-nueva-instrucción suma-con-siguiente como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      suma-con-siguiente;
      deja-zumbador;
    fin si-no
      avanza;
  fin;

  define-nueva-instrucción parejas-siguiente(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      parejas-siguiente(sucede(n));
    fin si-no inicio
      deja(n);
      atras;
      multiplica(n);
    fin;
  fin;

  define-nueva-instrucción cuenta-parejas como inicio
    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        si junto-a-zumbador entonces inicio
          deja-zumbador;
          parejas-propias(0);
          avanza;
          parejas-siguiente(0);
          norte;
          avanza;
          sur;
          suma-con-siguiente;
          este;
        fin si-no inicio
          deja-zumbador;
          avanza;
          parejas-siguiente(0);
        fin;
      fin;
      avanza;
    fin;
  fin;

  define-nueva-instrucción acumula como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      acumula;
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        acumula;
      fin;
    fin;
  fin;

  inicia-ejecución
    avanza;
    coge-todo;
    atras;
    este;
    coge-zumbador;
    cubetas;
    deja-zumbador;
    esquina;
    cuenta-parejas;
    gira(2);
    acumula;
    apágate;
  termina-ejecución
finalizar-programa
