iniciar-programa
  define-nueva-instrucción norte como
    mientras no-orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no-orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no-orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no-orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción uno-uno como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    este;
  fin;

  define-nueva-instrucción cubetas como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cubetas;
      avanza;
    fin si-no inicio
      avanza;
      si junto-a-zumbador entonces inicio
        cubetas;
        atras;
        deja-zumbador;
        esquina;
      fin si-no
        esquina;
    fin;
  fin;

  define-nueva-instrucción copia(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia(sucede(n));
      deja-zumbador;
    fin si-no inicio
      deja(n);
      avanza;
    fin;
  fin;

  define-nueva-instrucción copia-a-pared(n) como inicio
    si frente-libre y no si-es-cero(n) entonces inicio
      copia(0);
      copia-a-pared(precede(n));
      norte;
      copia(0);
      sur;
      avanza;
      coge-todo;
      este;
      avanza;
    fin si-no
      gira(2);
  fin;

  define-nueva-instrucción aprende-error-y-acumula(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      aprende-error-y-acumula(sucede(n));
    fin si-no inicio
      sur;
      avanza;
      este;
      mientras frente-libre hacer inicio
        si junto-a-zumbador entonces inicio
          norte;
          copia(0);
          oeste;
          copia-a-pared(n);
          norte;
          copia(0);
          sur;
          avanza;
          coge-todo;
          avanza;
          este;
        fin;
        avanza;
      fin;
    fin;
  fin;

  define-nueva-instrucción multiplica(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      multiplica(n);
      deja(n);
    fin;
  fin;

  define-nueva-instrucción divide(n) como inicio
    si junto-a-zumbador entonces inicio
      repetir n veces coge-zumbador;
      divide(n);
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción cuenta-y-multiplica(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-y-multiplica(sucede(n));
    fin si-no inicio
      sur;
      repetir 2 veces avanza;
      multiplica(n);
    fin;
  fin;

  define-nueva-instrucción quita(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      quita(sucede(n));
      coge-zumbador;
    fin si-no inicio
      deja(n);
      avanza;
    fin;
  fin;

  define-nueva-instrucción calcula-parejas como inicio
    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces inicio
        norte;
        copia(0);
        quita(0);
        cuenta-y-multiplica(0);
        este;
      fin;
      avanza;
    fin;
  fin;

  define-nueva-instrucción suma-todo como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      suma-todo;
      deja-zumbador;
    fin si-no inicio
      si frente-libre entonces inicio
        avanza;
        suma-todo;
      fin;
    fin;
  fin;

  inicia-ejecución
    este;
    cubetas;
    atras;
    deja-zumbador;
    esquina;
    norte;
    avanza;
    aprende-error-y-acumula(0);
    esquina;
    calcula-parejas;
    oeste;
    suma-todo;
    apágate;
  termina-ejecución
finalizar-programa
