iniciar-programa
  define-nueva-instrucción pared como inicio
    mientras frente-libre hacer avanza;
  fin;

  define-nueva-instrucción regresa(n) como inicio
    mientras no-orientado-al-oeste hacer gira-izquierda;
    pared;
    gira-izquierda;
    pared;
    gira-izquierda;
    gira-izquierda;
    si si-es-cero(precede(precede(n))) entonces inicio
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción siguiente como inicio
    si frente-libre entonces inicio
      avanza;
    fin si-no si derecha-libre entonces inicio
      gira-izquierda;
      gira-izquierda;
      pared;
      gira-izquierda;
      avanza;
      gira-izquierda;
    fin;
  fin;

  define-nueva-instrucción recorre(n) como inicio
    {*
          n = 0 no se agarro ningun zumbador
          n = 1 se agarro almenos 1 zumbador
          n = 2 se vacio una casilla
        *}
    si frente-bloqueado y derecha-bloqueada entonces inicio
      si si-es-cero(n) y no-junto-a-zumbador entonces inicio
        apágate;
      fin si-no inicio
        si junto-a-zumbador entonces inicio
          coge-zumbador;
          si no-junto-a-zumbador entonces inicio
            regresa(2);
          fin si-no inicio
            regresa(n);
          fin;
        fin si-no inicio
          regresa(n);
        fin;
      fin;
    fin si-no inicio
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        si no-junto-a-zumbador entonces inicio
          siguiente;
          recorre(2);
        fin si-no inicio
          siguiente;
          si si-es-cero(n) entonces inicio
            recorre(1);
          fin si-no inicio
            recorre(n);
          fin;
        fin;
      fin si-no inicio
        siguiente;
        recorre(n);
      fin;
    fin;
  fin;

  inicia-ejecución
    repetir 99 veces inicio
      siguiente;
      recorre(0);
    fin;
    apágate;
  termina-ejecución
finalizar-programa
