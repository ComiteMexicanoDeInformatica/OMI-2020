iniciar-programa
  define-nueva-instrucción gira(n) como inicio
    repetir n veces gira-izquierda;
  fin;

  define-nueva-instrucción altura(n) como inicio
    si frente-libre entonces inicio
      avanza;
      altura(sucede(n));
    fin si-no inicio
      gira(2);
      repetir precede(n) veces avanza;
      repetir n veces deja-zumbador;
      avanza;
    fin;
  fin;

  define-nueva-instrucción divide(n) como inicio
    gira(2);
    avanza;
    mientras junto-a-zumbador hacer inicio
      repetir n veces inicio
        si junto-a-zumbador entonces inicio
          coge-zumbador;
        fin si-no inicio
          gira(2);
          avanza;
          mientras junto-a-zumbador hacer coge-zumbador;
          sal-de-instrucción;
        fin;
      fin;
      gira(2);
      avanza;
      deja-zumbador;
      gira(2);
      avanza;
    fin;
    gira(2);
    avanza;
  fin;

  define-nueva-instrucción cuenta-divisor(n) como inicio
    si si-es-cero(n) y no junto-a-zumbador entonces sal-de-instrucción;
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cuenta-divisor(sucede(n));
    fin si-no inicio
      divide(n);
    fin;
  fin;

  define-nueva-instrucción siguiente como inicio
    altura(0);
    cuenta-divisor(0);
    si izquierda-libre entonces inicio
      gira-izquierda;
      avanza;
      gira-izquierda;
      siguiente;
    fin;
  fin;

  inicia-ejecución
    siguiente;
    gira(3);
    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        si no-junto-a-zumbador entonces apágate;
      fin;
      avanza;
      si frente-bloqueado entonces gira(2);
    fin;
    apágate;
  termina-ejecución
finalizar-programa
