iniciar-programa
  define-nueva-instrucción norte como inicio
    mientras no-orientado-al-norte hacer inicio
      gira-izquierda;
    fin;
  fin;

  define-nueva-instrucción este como inicio
    mientras no-orientado-al-este hacer inicio
      gira-izquierda;
    fin;
  fin;

  define-nueva-instrucción oeste como inicio
    mientras no-orientado-al-oeste hacer inicio
      gira-izquierda;
    fin;
  fin;

  define-nueva-instrucción sur como inicio
    mientras no-orientado-al-sur hacer inicio
      gira-izquierda;
    fin;
  fin;

  define-nueva-instrucción deja(n) como inicio
    repetir n veces inicio
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
      deja-zumbador;
    fin si-no inicio
      mientras junto-a-zumbador hacer inicio
        coge-zumbador;
      fin;
    fin;
  fin;

  define-nueva-instrucción baja(n) como inicio
    sur;
    avanza;
    si no-junto-a-zumbador entonces inicio
      deja(n);
    fin si-no inicio
      compara(n);
    fin;
    norte;
    avanza;
  fin;

  define-nueva-instrucción expandeIzq(n) como inicio
    si frente-libre entonces inicio
      si izquierda-libre entonces inicio
        baja(n);
        oeste;
      fin;
      avanza;
      expandeIzq(sucede(n));
      avanza;
    fin si-no inicio
      si izquierda-libre entonces inicio
        baja(n);
      fin;
      este;
    fin;
  fin;

  define-nueva-instrucción expandeDer(n) como inicio
    si frente-libre entonces inicio
      si derecha-libre entonces inicio
        baja(n);
        este;
      fin;
      avanza;
      expandeDer(sucede(n));
      avanza;
    fin si-no inicio
      si derecha-libre entonces inicio
        baja(n);
      fin;
      oeste;
    fin;
  fin;

  define-nueva-instrucción avanza-pared como inicio
    mientras frente-libre hacer inicio
      avanza;
    fin;
  fin;

  define-nueva-instrucción verifica-fin como inicio
    oeste;
    avanza-pared;
    este;
    mientras frente-libre y derecha-bloqueada hacer inicio
      avanza;
    fin;
    si derecha-bloqueada entonces inicio
      sur;
    fin si-no inicio
      oeste;
      avanza-pared;
      este;
    fin;
  fin;

  define-nueva-instrucción expande(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      expande(sucede(n));
    fin si-no inicio
      oeste;
      expandeIzq(n);
      este;
      expandeDer(n);
    fin;
  fin;

  define-nueva-instrucción expande-fila como inicio
    oeste;
    avanza-pared;
    este;

    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces inicio
        expande(0);
        este;
      fin;
      avanza;
    fin;
    si junto-a-zumbador entonces inicio
      expande(0);
    fin;
  fin;

  define-nueva-instrucción siguiente-fila como inicio
    oeste;
    avanza-pared;
    este;
    mientras derecha-bloqueada hacer inicio
      avanza;
    fin;
    sur;
    avanza;
  fin;

  define-nueva-instrucción encuentra-sig como inicio
    mientras no-junto-a-zumbador y frente-libre hacer inicio
      avanza;
    fin;
  fin;

  define-nueva-instrucción deja-minimo(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      deja-minimo(sucede(n));
    fin si-no inicio
      oeste;
      avanza-pared;
      si no-junto-a-zumbador entonces inicio
        deja(n);
      fin si-no inicio
        ;
        compara(n);
      fin;
    fin;
  fin;

  define-nueva-instrucción termina como inicio
    oeste;
    avanza-pared;
    este;
    encuentra-sig;
    mientras junto-a-zumbador y no orientado-al-sur hacer inicio
      deja-minimo(0);
      este;
      si frente-libre entonces inicio
        avanza;
        encuentra-sig;
      fin si-no inicio
        sur;
      fin;
    fin;

    oeste;
    avanza-pared;
    coge-zumbador;
  fin;

  inicia-ejecución
    deja-zumbador;
    verifica-fin;
    si orientado-al-sur entonces inicio
      oeste;
      avanza-pared;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
      fin;
      apágate;
    fin;

    mientras no-orientado-al-sur hacer inicio
      expande-fila;
      siguiente-fila;
      verifica-fin;
    fin;

    termina;
    apágate;
  termina-ejecución
finalizar-programa
