iniciar-programa
  define-prototipo-instrucción procesa-pintura(n);

  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    este;
  fin;

  define-nueva-instrucción super-avanza como inicio
    si frente-libre entonces
      avanza
    si-no inicio
      sur;
      pared;
      este;
      si frente-libre entonces inicio
        avanza;
        norte;
      fin;
    fin;
  fin;

  define-nueva-instrucción super-zumbador como inicio
    mientras no junto-a-zumbador y no orientado-al-este hacer super-avanza;
  fin;

  define-nueva-instrucción cubetas como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cubetas;
      avanza;
    fin si-no inicio
      super-zumbador;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        cubetas;
        deja-zumbador;
        esquina;
      fin si-no
        esquina;
    fin;
  fin;

  define-nueva-instrucción deja-mayor como inicio
    zumbador;
    si frente-libre entonces inicio
      avanza;
      zumbador;
      si junto-a-zumbador entonces inicio
        gira(2);
        coge-zumbador;
        avanza;
        zumbador;
        coge-zumbador;
        gira(2);
        deja-mayor;
      fin;
    fin;
  fin;

  define-nueva-instrucción deja-resultado como inicio
    si frente-libre entonces inicio
      avanza;
      deja-resultado;
      deja-zumbador;
    fin si-no
      coge-todo;
  fin;

  inicia-ejecución
    super-zumbador;
    coge-zumbador;
    cubetas;
    deja-zumbador;
    esquina;

    zumbador;
    avanza;
    zumbador;
    avanza;
    zumbador;
    coge-todo;
    gira(2);

    deja-mayor;

    esquina;
    zumbador;
    gira(2);

    deja-resultado;
    deja-zumbador;

    apágate;
  termina-ejecución
finalizar-programa
