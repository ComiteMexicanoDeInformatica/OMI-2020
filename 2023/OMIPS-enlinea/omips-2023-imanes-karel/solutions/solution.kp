iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    norte;
  fin;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción obten-diferencias(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      obten-diferencias(sucede(n));
      coge-zumbador;
    fin si-no inicio
      deja(n);
      si frente-libre entonces inicio
        avanza;
        obten-diferencias(0);
        avanza;
      fin si-no inicio
        coge-todo;
        este;
        avanza;
      fin;
    fin;
  fin;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
      deja-zumbador;
    fin si-no inicio
      si junto-a-zumbador entonces sur;
    fin;
  fin;

  define-nueva-instrucción compara-distancias(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      compara-distancias(sucede(n));
    fin si-no inicio
      avanza;
      si junto-a-zumbador entonces
        compara(n)
      si-no
        sur;
    fin;
  fin;

  define-nueva-instrucción quita-uno-a-todos como inicio
    mientras junto-a-zumbador y frente-libre hacer inicio
      coge-zumbador;
      avanza;
    fin;
    si junto-a-zumbador entonces coge-zumbador;
  fin;

  define-nueva-instrucción cuenta-cambios como inicio
    zumbador;
    si junto-a-zumbador entonces inicio
      espacio;
      si no junto-a-zumbador entonces inicio
        cuenta-cambios;
        deja-zumbador;
      fin si-no inicio
        esquina;
        deja-zumbador;
      fin;
    fin si-no
      esquina;
  fin;

  inicia-ejecución
    este;
    espacio;
    gira(2);
    si no junto-a-zumbador entonces avanza;
    norte;
    avanza;
    deja(2);
    atras;
    oeste;
    avanza;
    obten-diferencias(0);

    oeste;
    mientras frente-libre hacer inicio
      compara-distancias(0);
      si orientado-al-sur entonces inicio
        norte;
        avanza;
        deja(1);
      fin si-no inicio
        norte;
        avanza;
        deja(2);
      fin;
      atras;
      oeste;
    fin;

    norte;
    avanza;
    este;
    quita-uno-a-todos;
    oeste;
    pared;
    este;
    cuenta-cambios;

    apágate;
  termina-ejecución
finalizar-programa
