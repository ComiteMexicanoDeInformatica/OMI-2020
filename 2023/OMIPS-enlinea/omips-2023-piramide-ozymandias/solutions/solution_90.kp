iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción lado-izquierdo como inicio
    este;
    mientras frente-libre hacer inicio
      avanza;
      deja-zumbador;
    fin;
    gira(2);
    espacio;
    norte;
  fin;

  define-nueva-instrucción lado-derecho como inicio
    oeste;
    mientras frente-libre hacer inicio
      avanza;
      deja-zumbador;
    fin;
    gira(2);
    espacio;
    sur;
  fin;

  inicia-ejecución
    mientras frente-libre hacer inicio
      si orientado-al-norte entonces inicio
        lado-izquierdo;
        avanza;
        este;
        avanza;
        si derecha-bloqueada y frente-libre entonces inicio
          avanza;
          si derecha-libre entonces inicio
            sur;
          fin si-no inicio
            atras;
          fin;
        fin;
        si no orientado-al-sur entonces norte;
      fin;

      si orientado-al-sur entonces inicio
        avanza;
        lado-derecho;
        este;
        avanza;
        sur;
      fin;
    fin;

    apágate;
  termina-ejecución
finalizar-programa
