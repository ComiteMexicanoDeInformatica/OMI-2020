iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción expande-columna como inicio
    si junto-a-zumbador y frente-libre entonces inicio
      coge-zumbador;
      expande-columna;
      deja-zumbador;
      si frente-libre entonces avanza;
    fin si-no inicio
      norte;
    fin;
  fin;

  define-nueva-instrucción expande-alturas como inicio
    si junto-a-zumbador entonces inicio
      expande-columna;
      sur;
      pared;
      este;
    fin;

    si junto-a-zumbador y frente-libre entonces inicio
      avanza;
      expande-alturas;
    fin;
  fin;

  define-nueva-instrucción busca-mayor-altura como inicio
    zumbador;
    si no junto-a-zumbador entonces inicio
      este;
      pared;
      sur;
      avanza;
      oeste;
      busca-mayor-altura;
    fin;
  fin;

  define-nueva-instrucción cuenta como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      zumbador;
      si junto-a-zumbador entonces inicio
        cuenta;
        deja-zumbador;
      fin si-no inicio
        sur;
        pared;
        coge-todo;
      fin;
    fin;
  fin;

  inicia-ejecución
    (*Subtarea 4 ------------------------*)
    (* Esta subtarea busca que el alumno no requiera saber compara pero que haga la observación *)
    (* de que se debe contar el número de máximos *)

    expande-alturas;

    norte;
    pared;
    oeste;
    busca-mayor-altura;

    cuenta;

    apágate;
  termina-ejecución
finalizar-programa
