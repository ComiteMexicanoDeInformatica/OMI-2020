iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción compara(n) como inicio
    si junto-a-zumbador y no si-es-cero(n) entonces inicio
      coge-zumbador;
      compara(precede(n));
      deja-zumbador;
    fin si-no inicio
      si si-es-cero(n) entonces norte;
    fin;
  fin;

  define-nueva-instrucción cuenta como inicio
    si junto-a-zumbador y orientado-al-este entonces inicio
      compara(10);
      si orientado-al-norte entonces inicio
        este;
        si frente-libre entonces
          avanza
        si-no
          norte;
        cuenta;
        deja-zumbador;
      fin si-no inicio
        si frente-libre entonces
          avanza
        si-no
          norte;
        cuenta;
      fin;
    fin si-no inicio
      oeste;
      pared;
      coge-todo;
    fin;
  fin;

  inicia-ejecución
    (*Subtarea 3 ------------------------*)
    (*La energía máxima es 10, hay que contar la cantidad de 10's que hay*)
    este;
    cuenta;
    apágate;
  termina-ejecución
finalizar-programa
