iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    este;
  fin;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  inicia-ejecución
    mientras frente-libre o derecha-libre o izquierda-libre hacer inicio
      si orientado-al-este entonces inicio
        si frente-libre y derecha-libre entonces deja-zumbador;
        si izquierda-libre y derecha-libre entonces inicio
          deja-zumbador;
          gira(1);
        fin;
      fin si-no inicio
        si orientado-al-oeste entonces inicio
          si frente-libre y izquierda-libre entonces deja-zumbador;
          si izquierda-libre y derecha-libre entonces inicio
            deja-zumbador;
            gira(3);
          fin;
        fin si-no inicio
          si orientado-al-sur entonces inicio
            si frente-libre y izquierda-libre entonces inicio
              deja-zumbador;
              gira(1);
            fin si-no inicio
              si frente-libre y derecha-libre entonces inicio
                deja-zumbador;
                gira(3);
              fin;
            fin;
          fin;
        fin;
      fin;

      si frente-bloqueado entonces inicio
        si derecha-libre entonces
          gira(3)
        si-no
          gira(1);
      fin;
      avanza;
    fin;
    apágate;
  termina-ejecución
finalizar-programa
