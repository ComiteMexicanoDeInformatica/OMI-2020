iniciar-programa
  define-nueva-instrucción girar(n) como repetir n veces gira-izquierda;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción vertical(a) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      repetir a veces deja-zumbador;
      sal-de-instrucción;
    fin;
    si frente-libre entonces inicio
      avanza;
      vertical(sucede(a));
      si no junto-a-zumbador entonces avanza;
    fin si-no
      girar(2);
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción horizontal(s) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      repetir s veces deja-zumbador;
      sal-de-instrucción;
    fin;
    girar(1);
    vertical(s);
    si junto-a-zumbador entonces sal-de-instrucción;
    vertical(s);
    si junto-a-zumbador entonces sal-de-instrucción;
    girar(3);
    si frente-libre entonces inicio
      avanza;
      horizontal(sucede(s));
      si no junto-a-zumbador entonces avanza;
    fin si-no
      girar(2);
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción union como inicio
    horizontal(0);
    si junto-a-zumbador entonces sal-de-instrucción;
    horizontal(0);
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción divide(m) como inicio
    si junto-a-zumbador entonces inicio
      repetir m veces si junto-a-zumbador entonces coge-zumbador;
      divide(m);
      deja-zumbador;
    fin;
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción faltan-resta(z) como inicio
    si si-es-cero(z) entonces sal-de-instrucción;
    si no-junto-a-zumbador entonces inicio
      repetir z veces repetir 2 veces gira-izquierda;
      sal-de-instrucción;
    fin;
    coge-zumbador;
    faltan-resta(precede(z));
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción divide-ext(w) como inicio
    si junto-a-zumbador entonces inicio
      faltan-resta(w);
      divide-ext(w);
      deja-zumbador;
    fin si-no inicio
      si orientado-al-sur entonces deja-zumbador;
      mientras no orientado-al-norte hacer gira-izquierda;
    fin;
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción rectifica(q) como inicio
    si junto-a-zumbador entonces inicio
      divide(q);
      rectifica(q);
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción impares(q) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      impares(sucede(q));
    fin si-no inicio
      union;
      coge-zumbador;
      mientras no orientado-al-norte hacer gira-izquierda;
      divide-ext(q);
    fin;
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción verificar como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        verificar;
        repetir 2 veces deja-zumbador;
      fin si-no inicio
        mientras no-orientado-al-sur hacer gira-izquierda;
        apágate;
      fin;
    fin;
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción pares(l) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      pares(sucede(l));
    fin si-no inicio
      union;
      coge-zumbador;
      verificar;
      mientras no orientado-al-norte hacer gira-izquierda;
      divide(l);
    fin;
  fin;

  (*----------------------------------------------------------------------------*)
  define-nueva-instrucción quesoy(t) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        quesoy(sucede(t));
      fin si-no inicio
        repetir t veces repetir 2 veces deja-zumbador;
        deja-zumbador;
        impares(0);
      fin;
    fin si-no inicio
      repetir 2 veces repetir t veces deja-zumbador;
      pares(0);
    fin;
  fin;

  inicia-ejecución
    mientras no orientado-al-este hacer gira-izquierda;
    quesoy(0);
    apágate;
  termina-ejecución
finalizar-programa
