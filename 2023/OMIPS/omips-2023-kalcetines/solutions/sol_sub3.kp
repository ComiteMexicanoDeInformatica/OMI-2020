iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    este;
  fin;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción cubetas como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      cubetas;
      avanza;
    fin si-no inicio
      si frente-libre entonces avanza;
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        cubetas;
        deja-zumbador;
        esquina;
      fin si-no inicio
        esquina;
      fin;
    fin;
  fin;

  define-nueva-instrucción deja-resultado(n) como inicio
    si frente-libre entonces inicio
      avanza;
      deja-resultado(sucede(n));
    fin si-no inicio
      este;
      coge-todo;
      deja(n);
      avanza;
      coge-todo;
      deja(sucede(n));
      apágate;
    fin;
  fin;

  inicia-ejecución
    este;
    cubetas;
    mientras frente-libre hacer inicio
      si junto-a-zumbador entonces inicio
        avanza;
        si junto-a-zumbador entonces inicio
          oeste;
          deja-resultado(0);
        fin;
      fin;
      avanza;
    fin;
    apágate;
  termina-ejecución
finalizar-programa
