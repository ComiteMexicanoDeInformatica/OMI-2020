iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    este;
  fin;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción espacio como
    mientras junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción deja-resultado(n) como inicio
    esquina;
    deja(n);
    avanza;
    coge-todo;
    deja(n);
    apágate;
  fin;

  define-nueva-instrucción prueba(n) como inicio
    este;
    pared;
    oeste;
    zumbador;
    mientras junto-a-zumbador o
           (orientado-al-oeste y frente-libre) o
           (orientado-al-norte y izquierda-libre)
    hacer inicio
      si junto-a-zumbador entonces inicio
        coge-zumbador;
        si no junto-a-zumbador entonces inicio
          si orientado-al-norte entonces
            deja-resultado(n)
          si-no
            norte;
        fin;
      fin;

      si orientado-al-oeste entonces
        avanza
      si-no inicio
        oeste;
        avanza;
        norte;
      fin;
    fin;
    prueba(sucede(n));
  fin;

  inicia-ejecución
    prueba(1);
    apágate;
  termina-ejecución
finalizar-programa
