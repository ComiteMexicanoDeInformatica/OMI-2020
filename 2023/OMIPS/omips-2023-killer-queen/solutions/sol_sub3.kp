iniciar-programa
  define-prototipo-instrucción resuelve(n);

  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    este;
  fin;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción coge(n) como repetir n veces coge-zumbador;

  define-nueva-instrucción deja-todo como
    mientras algún-zumbador-en-la-mochila hacer deja-zumbador;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción avanza-diagonal como inicio
    avanza;
    gira(3);
    avanza;
    gira(1);
  fin;

  define-nueva-instrucción diagonal como inicio
    coge-todo;
    mientras frente-libre y derecha-libre y no junto-a-zumbador hacer inicio
      si algún-zumbador-en-la-mochila entonces deja-todo;
      avanza-diagonal;
    fin;
    si junto-a-zumbador entonces coge-zumbador;

    gira(2);
    mientras no junto-a-zumbador hacer avanza-diagonal;
    deja-todo;
  fin;

  define-nueva-instrucción recto como inicio
    coge-todo;
    mientras frente-libre y no junto-a-zumbador hacer inicio
      si algún-zumbador-en-la-mochila entonces deja-todo;
      avanza;
    fin;
    si junto-a-zumbador entonces coge-zumbador;
    gira(2);
    mientras no junto-a-zumbador hacer avanza;
    deja-todo;
  fin;

  inicia-ejecución
    repetir 4 veces inicio
      si frente-libre entonces
        recto
      si-no
        gira(2);
      si frente-libre y derecha-libre entonces
        diagonal
      si-no
        gira(2);
      gira-izquierda;
    fin;

    coge(1);

    apágate;
  termina-ejecución
finalizar-programa
