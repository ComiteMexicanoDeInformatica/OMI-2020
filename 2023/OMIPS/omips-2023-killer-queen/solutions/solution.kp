iniciar-programa
  define-prototipo-instrucción resuelve(n);

  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    este;
  fin;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción copia como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia;
      deja-zumbador;
    fin si-no
      avanza;
  fin;

  define-nueva-instrucción avanza-diagonal como inicio
    avanza;
    gira(3);
    avanza;
    gira(1);
  fin;

  define-nueva-instrucción copia-diagonal como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      copia-diagonal;
      deja-zumbador;
    fin si-no
      avanza-diagonal;
  fin;

  define-nueva-instrucción recto como inicio
    si frente-libre entonces inicio
      avanza;
      recto;
      copia;
    fin si-no
      gira(2);
  fin;

  define-nueva-instrucción diagonal como inicio
    si frente-libre y derecha-libre entonces inicio
      avanza-diagonal;
      diagonal;
      copia-diagonal;
    fin si-no
      gira(2);
  fin;

  inicia-ejecución
    coge-zumbador;
    repetir 4 veces inicio
      recto;
      diagonal;
      gira-izquierda;
    fin;
    apágate;
  termina-ejecución
finalizar-programa
