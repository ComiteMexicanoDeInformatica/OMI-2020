iniciar-programa
  define-nueva-instrucción norte como
    mientras no orientado-al-norte hacer gira-izquierda;

  define-nueva-instrucción sur como
    mientras no orientado-al-sur hacer gira-izquierda;

  define-nueva-instrucción este como
    mientras no orientado-al-este hacer gira-izquierda;

  define-nueva-instrucción oeste como
    mientras no orientado-al-oeste hacer gira-izquierda;

  define-nueva-instrucción gira(n) como repetir n veces gira-izquierda;

  define-nueva-instrucción pared como mientras frente-libre hacer avanza;

  define-nueva-instrucción esquina como inicio
    oeste;
    pared;
    sur;
    pared;
    este;
  fin;

  define-nueva-instrucción coge-todo como
    mientras junto-a-zumbador hacer coge-zumbador;

  define-nueva-instrucción deja(n) como repetir n veces deja-zumbador;

  define-nueva-instrucción zumbador como
    mientras no junto-a-zumbador y frente-libre hacer avanza;

  define-nueva-instrucción atras como inicio
    gira(2);
    avanza;
    gira(2);
  fin;

  define-nueva-instrucción diagonal_sube(n) como inicio
    repetir n veces inicio
      avanza;
      deja-zumbador;
      norte;
      avanza;
      oeste;
    fin;
  fin;

  define-nueva-instrucción diagonal_baja como inicio
    avanza;
    sur;
    si frente-libre entonces inicio
      avanza;
      deja-zumbador;
      oeste;
      diagonal_baja;
    fin si-no
      oeste;
  fin;

  define-nueva-instrucción mide_mitad(n) como inicio
    si orientado-al-sur entonces inicio
      avanza;
      si junto-a-zumbador entonces inicio
        este;
        coge-zumbador;
      fin;
      mide_mitad(sucede(n));
      deja-zumbador;
    fin si-no inicio
      avanza;
      si derecha-libre entonces inicio
        mide_mitad(sucede(n));
        deja-zumbador;
      fin;
    fin;
  fin;

  define-nueva-instrucción divide(n) como inicio
    si junto-a-zumbador entonces inicio
      repetir n veces coge-zumbador;
      divide(n);
      deja-zumbador;
    fin;
  fin;

  define-nueva-instrucción construye(n) como inicio
    si junto-a-zumbador entonces inicio
      coge-zumbador;
      construye(sucede(n));
    fin si-no inicio
      diagonal_sube(n);
      sur;
      avanza;
      oeste;
      diagonal_baja;
    fin;
  fin;

  define-nueva-instrucción arco como inicio
    pared;
    deja-zumbador;
    norte;
    mientras izquierda-bloqueada hacer avanza;
    gira(2);
    mide_mitad(0);
    deja(2);
    divide(2);
    gira(2);
    construye(0);
  fin;

  inicia-ejecución
    este;
    pared;
    sur;
    pared;
    oeste;
    mientras frente-libre hacer arco;
    apágate;
  termina-ejecución
finalizar-programa
